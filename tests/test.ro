[my_calc]

- test_count int

// asserts bool
# AssertB(expected bool, actual bool, $caller_line)
  test_count++

  if expected != actual
    print("error:", nil, $caller_line, nil, ": expected:", expected, "actual:", actual)
    exit(1)

// asserts int
# AssertI(expected int, actual int, $caller_line)
  test_count++

  if expected != actual
    print("error:", nil, $caller_line, nil, ": expected:", expected, "actual:", actual)
    exit(1)

// asserts float
# AssertF(expected float, actual float, $caller_line)
  test_count++

  if expected != actual
    print("error:", nil, $caller_line, nil, ": expected:", expected, "actual:", actual)
    exit(1)

//-----------------------------------------------------------------------------
# seven() int
  return 7

# add(x int, y int) int
  return x + y

# sub(x int, y int) int
  return x - y

# upper(s string) string
  return s

# foo(x int) int
  return 19
  - xx int
  if x == 10
      - y int
      y = 23
      xx = y
  return xx + 3

// nil return type
# bar()
  return

## Point
  - x int
  - y int

# get_point_y(p Point) int
  return p.y

# set_point_x(p *Point, x int)
  p.x = x

# set_point_y(p *Point, y int)
  p.y = y

- pt Point
- g_center = Point { y = 117 }
- g_P = Point { y = -238, x = 1178 }
- g_Q = g_P

// global pointer to function
- addfp = add

# twice(n *int)
  *n = *n * 2

- g_array [4]int

- gcount int
- gvar int
- g int = 39
    
# set_gvar() int
  gvar = 119

:: Color
  | R // 0
  | G // 1
  | B // 2

# main() int

  ---
    - id int
    id = 114
    AssertI(125, id + 11)

  ---
    - i int = 19
    AssertI(19, i)

  ---
    AssertI(42, 39 + 3)

  ---
    - id int
    id = 0
    AssertI(114, id + 114)

  ---
    AssertI(4422, 3129 + 1293)

  ---
    AssertI(5533, 3129 + 1293+1111)

  ---
    AssertI(42, 20+22)

  ---
    - a int
    a = 12
    AssertI(12, a)

  ---
    - a int
    a = 11
    AssertI(11, a)

  ---
    AssertB(false, 12 == 11)

  ---
    AssertB(true, 42 == 42)

  ---
    - a int
    a = 39
    AssertB(true, a == 39)

  ---
    - a int
    a = 39
    AssertB(true, a == 39)

  ---
    AssertI(7, seven())

  ---
    AssertI(42, seven() + 35)

  ---
    AssertI(42, seven() + add(30, 5))

  ---
    - a int
    - b int
    a = 42
    if a == 12
      b = 11
    or
      b = 22
    AssertI(22, b)

  ---
    - a int
    - b int
    a = 42
    if a == 42
      b = 11
    or
      b = 22
    AssertI(11, b)

  ---
    - a int
    - b int
    a = 42
    if a == 42
      b = 1
    or
      b = 0
    AssertI(1, b)

  ---
    - a int
    - b int
    a = 42
    if a == 41
      b = 1
    or
      b = 0
    AssertI(0, b)

  // if statement
  // line comment at beginning of line
  ---
    - a int
    - b int
   // comment with incorrect indetation
    a = 42 // comment after vaid statement
    if a == 42
      b = 1
    or
      b = 0
    // comment with the same indetation
    AssertI(1, b)

  ---
    // if statement
    // line comment at beginning of line
    - a int
    - b int
  // comment with incorrect indetation
    a = 42 // comment after vaid statement
    if a == 42
      b = seven()
    or
      b = 0
    // comment with the same indetation
    AssertI(7, b)

  ---
    - s string
    - a = 33
    AssertI(33, a)

  ---
    - a int
    a = 42
    if a == 42
        - b int
        b = 13
   
        if  b == 13
            - c int
            c = 9
   
    //b = 4 // error
    a = 31
    AssertI(31, a)

  ---
    - a = seven() + add(30, 5)
    AssertI(42, a)

  ---
    AssertI(0, gvar)
    set_gvar()
    AssertI(119, gvar)

  ---
    AssertI(42, foo(10) + add(20, 3))

  ---
    - a int
    pt.x = 2
    pt.y = 3
    a = pt.y
    AssertI(5, pt.x + pt.y)

  ---
    - f float
    f = 3.14
    AssertF(3.14, f)

  ---
    AssertI(25, 0xF + 0Xa)

  ---
    - f float
    - g float
    f = 3.14
    g = 0.86
    AssertF(4.0, f + g)

  ---
    - i int
    if 13 == 13
      i = 42
    or
      i = 99
    AssertI(42, i)

  ---
    - s0 string
    - s1 string
    - str string
    - i = 0
    s0 = "Hello, "
    s1 = "World!"
    str = s0 + s1
    if str == "Hello, World!"
      i = 42
    or
      i = 0
    AssertI(42, i)

  ---
    - i int
    if 42 != 42
      i = 0
    or
      i = 11
    AssertI(11, i)

  ---
    // '-' operator and order of eval args
    - i = sub(12, 7)
    AssertI(5, i)

  ---
    // '*' operator
    - i int
    - j int
    i = 12
    j = 3
    AssertI(10, 46 - i * j)

  ---
    // '/' operator
    - i int
    - j int
    i = 12
    j = 3
    AssertI(42, 46 - i / j)

  ---
    // '%' operator
    - i int
    - j int
    i = 19
    j = 7
    AssertI(41, 46 - i % j)

  ---
    // '(' expr ')'
    - i int
    - j int
    i = 19
    j = 17
    AssertI(42, 21 * (i - j))

  ---
    // "||" operator
    - i int
    - j int
    i = 0
    j = 7
    AssertI(1, int((i || j) != 0))

  ---
    // "||" operator
    - i int
    - j int
    i = 0
    j = 0
    AssertI(0, int((i || j) != 0))

  ---
    // "&&" operator
    - i int
    - j int
    i = 0
    j = 7
    AssertI(0, int((i && j) != 0))

  ---
    // "&&" operator
    - i int
    - j int
    i = 1
    j = 7
    AssertI(1, int((i && j) != 0))

  ---
    // "+" unary operator
    - i int
    i = 7
    AssertI(7, +i)

  ---
    // "-" unary operator
    - i int
    i = 42
    AssertI(-42, -i)

  ---
    // "-+" unary operator
    - i int
    i = 42
    AssertI(-42, -+-+-+- -+-+ +-i)

  ---
    // "!" unary operator
    - i int
    i = 42
    AssertI(1, int(int(!(42 != i)) != 0))

  ---
    // "!" unary operator
    - i int
    i = 42
    AssertI(0, int((!i) != 0))

  ---
    // "<" operator
    - i int
    i = 42
    AssertI(0, int(int(i < 5) != 0))

  ---
    // "<=" operator
    - i int
    i = 42
    AssertI(1, int(int(i <= 42) != 0))

  ---
    // ">" operator
    - i int
    i = 42
    AssertI(1, int(int(i > 5) != 0))

  ---
    // ">=" operator
    - i int
    i = 42
    AssertI(1, int(int(i >= 42) != 0))

  ---
    // "++" operator
    - i int
    i = 42
    i++
    AssertI(43, i)

  ---
    // "--" operator
    - i int
    i = 42
    i--
    AssertI(41, i)

  ---
    // "&" operator
    - i int
    i = 0x3A9
    AssertI(0xA9, i & 0xFF)

  ---
    // "|" operator
    - i int
    i = 0x3A9
    AssertI(0xFFF, i | 0xFFF)

  ---
    // "~" operator
    - i int
    i = 0x8
    AssertI(0x7, ~i & 0xF)

  ---
    // "^" operator
    - i int
    i = 0x78
    AssertI(0x88, i ^ 0xF0)

  ---
    // "<<" operator
    - i int
    i = 0x8
    AssertI(0x40, i << 3)

  ---
    // ">>" operator
    - i int
    i = 0x8F
    AssertI(0x08, i >> 4)

  ---
    // "for" statment
    - i int
    - j int
    j = 0
    for i = 0; i < 10; i++
        j = j + 2
    AssertI(20, j)

  ---
    // "for" statment while style
    - i int
    i = 0
    for i < 10
        i++
    AssertI(10, i)

  ---
    // "for" statment infinite loop
    - i int
    i = 0
    for
      i++
      if i == 8
        break
    AssertI(8, i)

  ---
    // "break" statment
    - i int
    for i = 0; i < 10; i++
      if i == 5
        break
    AssertI(5, i)

  ---
    // "continue" statment
    - i int
    - j int
    j = 0
    for i = 0; i < 10; i++
      if i % 2 == 0
        continue
      j++
    AssertI(5, j)

  ---
    // "switch" statment
    - i int
    - j int
    i = 2
    j = 0
    switch i
    case 0
      j = 0
    case 1
      j = 23
    case 2
      j = 34
    case 3
      j = 77
    AssertI(34, j)

  ---
    // "default" statment
    - i int
    - j int
    i = 5
    j = 0
    switch i
    case 0
      j = 0
    case 1
      j = 23
    case 2
      j = 34
    case 3
      j = 77
    default
      j = 99
    AssertI(99, j)

  ---
    // local var init
    - i int = 42
    AssertI(42, i)

  ---
    // global var init
    AssertI(39, g)

  ---
    // local var type
    - i = 41
    AssertI(41, i)

  ---
    // local var type
    - i = 41
    - f = 3.1415
    - g float = 3.1
    if f == g + 0.0415
      i = 9
    AssertI(9, i)

  ---
    // "+=" operator
    - i = 42
    i += 4
    AssertI(46, i)

  ---
    // "-=" operator
    - i = 42
    i -= 4
    AssertI(38, i)

  ---
    // "*=" operator
    - i = 42
    i *= 4
    AssertI(168, i)

  ---
    // "/=" operator
    - i = 42
    i /= 4
    AssertI(10, i)

  ---
    // "%=" operator
    - i = 42
    i %= 4
    AssertI(2, i)

  ---
    // bool type
    - i = 42
    - b = true
    if b
      i = 19
    AssertI(19, i)

  ---
    // bool type
    - i = 42
    - b = true
    - c = false
    if b != !c
      i = 19
    AssertI(42, i)

  ---
    // nop statement
    - i int
    for i = 0; i < 7; i++
      nop
    AssertI(7, i)

  ---
    // block comment
    - i = 42   // int
    /*
      this is a block comment
      the first indent has to match.
    /*
        nested block comment.
    */
    this is another line in block comment.
    */
    AssertI(42, i)

  ---
    // char literal
    - i = 'a'
    AssertI(97, i)

  ---
    // char literal
    - i = '\n'
    AssertI(10, i)

  ---
    // slash at the end of string literal
    - i int
    - s = "Hello\\\\"
    if s == "Hello\\\\"
      i = 13
    AssertI(13, i)

  ---
    // nil return type
    - i = 11
    bar()
    AssertI(11, i)

  ---
    // scope statement
    - i = 17
    ---
      - i int
      i = 9
    AssertI(17, i)

  // C++ test
  // ==========================================================================

  // switch with multi case values
  ---
    - n = 6
    - x = 0

    switch n
    case 1
      x = 11
    case 2, 3, 4
      x = 22
    case 13, 55, 6
      x = 99
    default
      x = 255
    AssertI(99, x)

  // switch with multi case values
  ---
    - n = 3
    - x = 0

    switch n
    case 1
      x = 11
    case 2, 3, 4
      x = 22
    case 13, 55, 6
      x = 99
    default
      x = 255
    AssertI(22, x)

  // bool default
  ---
    - b bool
    - c = true
    AssertB(true, b == false)
    AssertB(true, c != false)

  // pointer variable
  ---
    - i int = 42
    - p *int
    p = &i
    AssertI(42, *p)

  // pointer argument
  ---
    - i int = 13
    twice(&i)
    AssertI(26, i)

  // array variable
  ---
    - a [8]int
    - i = 9
    a[2] = 87
    AssertI(87, a[2])
    a[5] = 92
    AssertI(92, a[5])
    i = a[2] + a[5]
    AssertI(179, i)

  // array variable with const expression
  ---
    - a [2 + 8]int
    - i = 7
    a[2] = 8
    AssertI(8, a[2])
    a[5] = 2
    AssertI(2, a[5])
    i = a[2] + a[5]
    AssertI(10, i)

  // global array variable
  ---
    g_array[3] = 99
    AssertI(99, g_array[3])

  // table
  ---
    - a = Color.R
    AssertI(0, a)
    AssertI(1, Color.G)
    AssertI(3, a + Color.G + Color.B)

  // module import
  ---
    AssertF(3.1415, my_calc.PI)
    my_calc.PI = 1.23
    AssertF(1.23, my_calc.PI)
    AssertI(42, my_calc.add(31, 11))

  // for zero times loop
  ---
    - i int
    - a = 13
    for i = 0; i < 0; i++
      a *= 2
    AssertI(13, a)

  // global pointer to function
  ---
    AssertI(2222, addfp(19, 2203))

  // local pointer to function
  ---
    - twicefp = twice
    - xxxxxxxxxxxxxxxxxxxxxxxxxxxx = -128
    twicefp(&xxxxxxxxxxxxxxxxxxxxxxxxxxxx)
    AssertI(-256, xxxxxxxxxxxxxxxxxxxxxxxxxxxx)

  // array initialization
  ---
    - a = [99, 11, 22, 33 + 9]
    AssertI(64, a[2] + a[3])

  // struct initialization
  ---
    - p = Point { x = 99, y = 11 }
    AssertI(99, p.x)
    AssertI(11, p.y)

    - q = Point { y = p.x - 49, x = p.y * 2 }
    AssertI(22, q.x)
    AssertI(50, q.y)

    - r = Point {
      x = q.x - 9,
      y = q.y / 2
    }
    AssertI(13, r.x)
    AssertI(25, r.y)

    - s = Point { y = 49 }
    AssertI(0, s.x)
    AssertI(49, s.y)

  // struct default initialization
  ---
    - p Point
    AssertI(0, p.x)
    AssertI(0, p.y)

  // struct initialization with another struct
  ---
    - p = Point { x = 31, y = 79 }
    - q = p
    AssertI(31, q.x)
    AssertI(79, q.y)

  // global struct initialization
  ---
    AssertI(0,   g_center.x)
    AssertI(117, g_center.y)

  // global struct initialization
  ---
    AssertI(1178,   g_Q.x)
    AssertI(-238, g_Q.y)

  // struct assignment
  ---
    - p = Point { x = -3, y = 1090 }
    AssertI(-3, p.x)
    AssertI(1090, p.y)

    - q = Point { y = 613 }
    AssertI(0, q.x)
    AssertI(613, q.y)

    q = p
    AssertI(-3, q.x)
    AssertI(1090, q.y)

  // struct parameter
  ---
    - p = Point { x = 319, y = -123 }
    AssertI(319, p.x)
    AssertI(-123, p.y)
    AssertI(-123, get_point_y(p))

  // struct reference parameter
  ---
    - p = Point { x = 37, y = 79 }
    AssertI(37, p.x)
    AssertI(79, p.y)

    set_point_x(&p, 19)
    AssertI(19, p.x)
    set_point_y(&p, 4299)
    AssertI(4299, p.y)

  //------------------------------
  print(test_count, "tests done.")

  return 0
